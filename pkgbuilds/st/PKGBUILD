pkgname=bhiggins-st
_appname='st'
pkgver=0.8.4
pkgrel=1
pkgdesc='My st configuration'
arch=('x86_64')
url='https://st.suckless.org/'
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext')
provides=('st')
conflicts=('st')
_patches=(
          "https://st.suckless.org/patches/scrollback/st-scrollback-20201205-4ef0cbd.diff"
          "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20191024-a2c479c.diff"
          "https://st.suckless.org/patches/ligatures/0.8.3/st-ligatures-scrollback-20200430-0.8.3.diff"
          "file://st.patch"
)
source=("https://dl.suckless.org/$_appname/$_appname-$pkgver.tar.gz"
        "${_patches[@]}")
sha256sums=(
            'd42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6'
            '3b8c7d1815352cbfa2e100f6bb65e4c7d5a338952a6e7513b59a6a6297f32fb4'
            '319458d980195d18fa0f81a6898d58f8d046c5ff982ab872d741f54bb60e267d'
            'd8adb94a880e29c615bea67acda27c4f4f2e1e7eb90909f6d19976a6181fef30'
            'SKIP'
)

prepare() {
    cd "$_appname-$pkgver"
    for patch in "${_patches[@]}"; do
        patch -p1 -i "$srcdir/$(basename $patch)" --backup --version-control numbered
    done
}

build() {
    cd "$_appname-$pkgver"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "$_appname-$pkgver"
    make DESTDIR="$pkgdir/" install
}       
